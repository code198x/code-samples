10 REM *** TEXT ADVENTURE PARSER - LESSON 13 ***
20 DIM IT$(5), RM$(3)
30 GOSUB 9000: REM SETUP
40 R = 1: REM START ROOM
50 PRINT CHR$(147)
60 PRINT RM$(R)
70 PRINT
80 PRINT "YOU SEE: ";
90 FOR I = 1 TO 5
100   IF IT$(I) <> "" THEN PRINT IT$(I); " ";
110 NEXT I
120 PRINT
130 PRINT
140 INPUT "COMMAND"; C$
150 GOSUB 1000: REM PARSE COMMAND
160 GOTO 50
1000 REM *** PARSE COMMAND ***
1010 IF LEN(C$) = 0 THEN PRINT "SAY SOMETHING!": RETURN
1020 REM FIND SPACE
1030 SP = 0
1040 FOR I = 1 TO LEN(C$)
1050   IF MID$(C$, I, 1) = " " THEN SP = I
1060 NEXT I
1070 IF SP = 0 THEN PRINT "SAY VERB AND NOUN!": RETURN
1080 REM EXTRACT VERB AND NOUN
1090 VB$ = LEFT$(C$, SP - 1)
1100 NO$ = RIGHT$(C$, LEN(C$) - SP)
1110 REM PROCESS COMMAND
1120 IF VB$ = "GET" THEN GOSUB 2000: RETURN
1125 IF VB$ = "TAKE" THEN GOSUB 2000: RETURN
1130 IF VB$ = "DROP" THEN GOSUB 3000: RETURN
1140 IF VB$ = "EXAMINE" THEN GOSUB 4000: RETURN
1145 IF VB$ = "LOOK" THEN GOSUB 4000: RETURN
1150 PRINT "I DON'T UNDERSTAND '"; VB$; "'"
1160 RETURN
2000 REM *** GET ITEM ***
2010 FOR I = 1 TO 5
2020   IF IT$(I) = NO$ THEN PRINT "YOU TAKE THE "; NO$: IT$(I) = "": RETURN
2030 NEXT I
2040 PRINT "THERE'S NO "; NO$; " HERE!"
2050 RETURN
3000 REM *** DROP ITEM ***
3010 PRINT "YOU DROP THE "; NO$
3020 FOR I = 1 TO 5
3030   IF IT$(I) = "" THEN IT$(I) = NO$: RETURN
3040 NEXT I
3050 RETURN
4000 REM *** EXAMINE ITEM ***
4010 IF NO$ = "SWORD" THEN PRINT "A RUSTY BLADE": RETURN
4020 IF NO$ = "KEY" THEN PRINT "AN OLD BRASS KEY": RETURN
4030 PRINT "NOTHING SPECIAL ABOUT THE "; NO$
4040 RETURN
9000 REM *** SETUP ***
9010 RM$(1) = "YOU ARE IN A DARK CAVE"
9020 RM$(2) = "YOU ARE IN A FOREST"
9030 RM$(3) = "YOU ARE AT A RIVER"
9040 IT$(1) = "SWORD"
9050 IT$(2) = "KEY"
9060 IT$(3) = "POTION"
9070 RETURN
